# Maintainer: moviuro <moviuro+archlinux@gmail.com>
# Contributor: D. Can Celasun <dcelasun[at]gmail[dot]com>
# Contributor: Sebastien Duthil <duthils@free.fr>
# Contributor: mickael9 <mickael9@gmail.com>
pkgname=factorio-experimental
pkgver=1.1.77
pkgrel=1
pkgdesc="A 2D game about building and maintaining factories (experimental branch)."
arch=('x86_64')
url="http://www.factorio.com/"
license=('custom: commercial')
conflicts=('factorio' 'factorio-demo')
provides=("factorio=$pkgver")
depends=('libxcursor' 'gcc-libs' 'alsa-lib' 'libxrandr' 'libxinerama' 'mesa')
# requires aur/factorio-dl or the game file directly next to the PKGBUILD
#DLAGENTS+=('factorio::/usr/bin/factorio-dl -o %o %u')
sha256sums=('69f27fca743656f250f4ae9322c53aa1f470fd80f11349f0f3004faf43b992e1'
            '5f62aa7763f9ad367a051371bc16f3c174022bb3380eb221ba06bac395bf9815')
options=(!strip)
# disable package compression - 0.17.32 takes around 385s on my i5-6600K@4.1GHz,
# and decompression takes another 25s, but saves some 200MB (xz -T0)
PKGEXT='.pkg.tar'
source=("factorio_alpha_x64_${pkgver}.tar.xz::factorio://${pkgver}"
        factorio.desktop)
# no modifications needed, the executable looks for:
# - data in /usr/share/factorio
# - config in ~/.factorio
package() {
  cd "${srcdir}/factorio"
  install -d "${pkgdir}/usr/bin"
  install -d "${pkgdir}/usr/share/applications"
  install -d "${pkgdir}/usr/share/factorio"
  install -d "${pkgdir}/usr/share/licenses/factorio"

  install -m755 "bin/x64/factorio" "${pkgdir}/usr/bin/factorio"
  cp -lr data/* "${pkgdir}/usr/share/factorio"
  install -m644 "${srcdir}/factorio.desktop" "${pkgdir}/usr/share/applications/factorio.desktop"
  install -m644 "${srcdir}/factorio/data/eula.txt" "${pkgdir}/usr/share/licenses/factorio/LICENSE"
}
